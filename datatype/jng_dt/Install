; Datatype installer - Chris Young

(procedure p_copysobj #sobj
	(set #sobj-version (getversion (tackon "data/SObjs/" #sobj)))

	(if (= #sobj-version 0)
		(
			(transcript "Date compare copy " #sobj)

			(if (exists (tackon "SObjs:" #sobj))
				(
					(set #file-newer (earlier (tackon "SObjs:" #sobj) (tackon "data/SObjs/" #sobj)))

					(if (= #file-newer 0)
						(
							(set #newer-text "has an older datestamp")
						)
						;else
						(
							(set #newer-text "has a newer datestamp")
						)
					)

					(set #old-size (getsize (tackon "SObjs:" #sobj)))
					(set #new-size (getsize (tackon "data/SObjs/" #sobj)))

					(if (AND (= #old-size #new-size) (= #file-newer 0))
						(
							(set #copy 0)
						)
						;else
						(
							(if (OR (= @user-level 2) (AND (= @user-level 1) (<> #file-newer 0)))
; Expert users are always prompted
; Average users are prompted if the file trying to be copied is newer
; Novice users are never prompted
; This is roughly equivalent to (copylib (confirm))
; No prompting occurs if the destination does not exist (silent copy)
; or the files are the same size and the one being copied isn't newer (don't copy)
								(
									(set #copy
										(askbool
											(prompt "Copying " #sobj "...\n\n"
												"Version to install: " #new-size " bytes\n"
												"Version currently installed: " #old-size " bytes\n\n"
												"The file to copy " #newer-text)
											(help @askbool-help)
											(default #file-newer)
											(choices "Proceed with copy" "Skip this part")
										)
									)
								)
								;else
								(
									(set #copy #file-newer)
								)
							)
						)
					)
				)
				; else if dest file does not exist
				(
					(set #copy 1)
				)
			)

			(if (<> #copy 0)
				(
					(if (<> #AutoInstall 1)
						(
							(copyfiles
								(prompt "Copying " #sobj "...")
								(help @copyfiles-help)
								(source (tackon "data/SObjs/" #sobj))
								(dest "SObjs:")
								(optional "nofail" "force")
							)
						)
						;else
						(
							(run "CopyStore data/SObjs/" #sobj " SObjs:")
						)
					)
				)
			)
		)
		; else if version info is available
		(
			(if (<> #AutoInstall 1)
				(
					(copylib
						(prompt "Copying " #sobj "...")
						(help @copylib-help)
						(source (tackon "data/SObjs/" #sobj))
						(dest "SObjs:")
						(optional "nofail" "force")
						(confirm "expert")
					)
				)
				;else
				(
					(run "CopyStore data/SObjs/" #sobj " SObjs:")
				)
			)
		)
	)
)

(procedure p_backupdesc #desc
	(copyfiles
		(prompt "Backing up Datatype descriptor " #desc)
		(help @copyfiles-help)
		(source (tackon "Devs:Datatypes" #desc))
		(dest "SYS:Storage/Datatypes")
		(infos)
		(noposition)
		(optional "nofail" "askuser")
	)
)

(procedure p_copydesc #desc
	(copyfiles
		(prompt "Copying Datatype descriptor " #desc)
		(help @copyfiles-help)
		(source (tackon "data" #desc))
		(dest "Devs:Datatypes")
		(infos)
		(noposition)
		(optional "force")
	)
)

(if (= @app-name "JNGDTAutoInstall") (set #AutoInstall 1))

(if (<> #AutoInstall 1) (welcome))

(set #defbitmask 7)

(if (exists "Devs:Datatypes/JNG")
	(
		(set #hasjngdesc 1)
		(set #defbitmask (- #defbitmask 1))
	)
)
(if (exists "Devs:Datatypes/MNG")
	(
		(set #hasmngdesc 1)
		(set #defbitmask (- #defbitmask 2))
	)
)
(if (exists "Devs:Datatypes/PNG")
	(
		(set #haspngdesc 1)
		(set #defbitmask (- #defbitmask 4))
	)
)

(set #user-choices
	(askoptions
		(prompt "Select the descriptors to install")
		(help "Items not ticked by default already exist on your system. "
				"These may be alternative PNG, MNG or JNG datatypes or a "
				"previous installation of jng.datatype.  If you select for "
                "them to be installed, the existing versions will be moved to "
                "SYS:Storage/Datatypes and jng.datatype will be used in the "
				"future for those formats.\n\n" @askoptions-help)
		(choices "JNG" "MNG" "PNG")
		(default #defbitmask)
	)
)

(if (BITAND #user-choices 1)
	(
		(if #hasjngdesc (p_backupdesc "JNG"))
		(p_copydesc "JNG")
	)
)

(if (BITAND #user-choices 2)
	(
		(if #hasmngdesc (p_backupdesc "MNG"))
		(p_copydesc "MNG")
	)
)

(if (BITAND #user-choices 4)
	(
		(if #haspngdesc (p_backupdesc "PNG"))
		(p_copydesc "PNG")
	)
)

(if (<> #AutoInstall 1)
	(
		(copylib
			(prompt "Copying datatype")
			(help @copylib-help)
			(source "data/jng.datatype")
			(dest "SYS:Classes/Datatypes")
		)
	)
;else
	(
		(run "CopyStore data/jng.datatype SYS:Classes/Datatypes")
	)
)


(p_copysobj "libmng.so.1")
(p_copysobj "libjpeg.so")

(run "adddatatypes refresh")

(if (= #AutoInstall 1)
	(
		(exit (quiet))
	)
	(
		(exit)
	)
)

